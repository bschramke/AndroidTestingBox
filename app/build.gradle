apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    buildToolsVersion '27.0.3'
    compileSdkVersion 27
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "com.github.bschramke.android.testing.sample"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"

        vectorDrawables {
            useSupportLibrary = true
        }

        testApplicationId "com.github.bschramke.android.testing.sample.cucumber"
        testInstrumentationRunner "com.github.bschramke.android.testing.sample.cucumber.CucumberAndroidInstrumentation"
    }
    dataBinding {
        enabled true
    }
    buildTypes {
        debug {
            testCoverageEnabled true
            buildConfigField "String", "TEST_TAGS", "\"${getTestTags()}\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest {
            assets.srcDirs = ['src/androidTest/assets']
        }
        //ensure that each java sourceSet also contains the kotlin folder
        all { sourceSet ->
            sourceSet.java.srcDir("src/${sourceSet.name}/kotlin")
        }
    }

}

ext.tatarkaReduxVersion = '0.10'
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'android.arch.lifecycle:extensions:1.1.1'

    implementation "me.tatarka.redux:redux-core:$tatarkaReduxVersion"
    implementation "me.tatarka.redux:redux-android:$tatarkaReduxVersion"
    implementation "me.tatarka.redux:redux-android-lifecycle:$tatarkaReduxVersion"
    implementation "me.tatarka.redux:redux-thunk:$tatarkaReduxVersion"
    implementation "me.tatarka.redux:redux-rx2:$tatarkaReduxVersion"

    testImplementation 'junit:junit:4.12'
    // Android Testing Support Library's runner and rules and hamcrest matchers
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'org.assertj:assertj-core:3.10.0'
    androidTestImplementation 'com.squareup.assertj:assertj-android:1.2.0'

    androidTestImplementation 'io.cucumber:cucumber-android:3.0.2'
    androidTestImplementation 'io.cucumber:cucumber-picocontainer:3.0.2'

    // Screenshots
    androidTestImplementation 'com.squareup.spoon:spoon-client:1.7.1'

    kapt "com.android.databinding:compiler:$androidPluginVersion"
}

// Task to retrieve Cucumber test reports
// The report path in this task depends on the path used in CucumberTestCase.java under @CucumberOptions
// The report path for emulator and real device differ depending on read permissions
// and SD-card presence respectively
gradle.taskGraph.afterTask { task, state ->
    if ((task == connectedAndroidTest) || (task == connectedCheck)) {
        def reportDir = new File(project.buildDir, "cucumber-reports")
        if (!reportDir.exists()) {
            reportDir.mkdirs()
        }
        println 'Retrieving Cucumber test report to ' + project.buildDir
        def processBuilder = new ProcessBuilder(
                ['adb', 'pull', '/mnt/sdcard/cucumber-reports/', reportDir.getAbsolutePath()])
        processBuilder.start()
    }
}

def getTestTags() {
    return project.hasProperty("tags") ? project.getProperties().get("tags") : ""
}
